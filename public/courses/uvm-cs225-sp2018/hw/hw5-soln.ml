(* Name: <your name> *)

(* Course: UVM CS 225 Spring 2018 - Darais *)
(* HW: HW5 *)

open Util
open StringSetMap

(* The Assignment:
 * 
 * Fill in the `raise TODO` parts of the code:
 * - 22 cases in the `step` function
 * - 07 cases in the `scope_ok` function
 * - 08 cases in the `infer` function
 *
 * See the writeup for the specification for `step`, `scope_ok` and `infer`
 * functions that you must implement.
 *
 * Passing all of the tests does not guarantee 100%. You may want to write some
 * tests of your own.
 *
 * The last case of `infer` is for unpack expressions. In both the writeup and
 * code we define an alternative (and equivalent) rule [Unpack-Alt] which is
 * more suggestive of an implementation than the original rule [Unpack].
 *
 * If you ever need to compare two types for equality, use the function tequal,
 * and not (=). tequal will compare types for alpha-equivalence, e.g., âˆ€X.X and
 * âˆ€Y.Y are alpha equivalent, so tequal (âˆ€Y.Y) (âˆ€X.X) will return true, whereas
 * (âˆ€Y.Y) = (âˆ€X.X) using OCaml's (=) operation will return false.
 *
 * You may find the following functions helpful:
 *
 * The empty string set: 
 *   
 *   on paper: {}
 *   in OCaml: StringSet.empty
 *
 * The singleton string set:
 *
 *   on paper: {x}
 *   in OCaml: StringSet.singleton x
 *   or      : StringSet.of_list [x]
 *
 * String set union:
 *
 *   on paper: Sâ‚ âˆª Sâ‚‚
 *   in OCaml: StringSet.union s1 s2
 *
 * Removing from a string set:
 *
 *   on paper: S \ {x}
 *   in OCaml: StringSet.remove x s
 *
 * Testing membership in a string set:
 *
 *   on paper: x âˆˆ S
 *   in OCaml: StringSet.mem x s
 *
 * Analogously for maps:
 *
 *   on paper: {}
 *   in OCaml: StringMap.empty
 *
 *   on paper: {x â†¦ Ï„}
 *   in OCaml: StringMap.singleton x t
 *
 *   on paper: Î“[x â†¦ Ï„]
 *   in OCaml: StringMap.add x t g
 *
 *   on paper Î“(x) = Ï„
 *   in OCaml: StringMap.find x g = t
 *
 * Remember to always use OCaml's structural equality (=), and never use
 * physical equality (==).
 *)

(* Syntax for Types.
 *
 * X âˆˆ tvar â‰ˆ ð•Š
 * Ï„ âˆˆ type â©´ bool | nat | Ï„ Ã— Ï„ | Ï„ â†’ Ï„ | X | âˆ€X.Ï„ | âˆƒX.Ï„
 *)

type tvar = string
[@@deriving show {with_path = false}]

type ty =
  | Bool
  | Nat
  | Prod of ty * ty
  | Fun of ty * ty
  | TVar of tvar
  | Forall of tvar * ty
  | Exists of tvar * ty
[@@deriving show {with_path = false}]

(**************************
 * Syntax for expressions *
 **************************)

(* x âˆˆ var â‰ˆ ð•Š
 *)
type var = string
[@@deriving show {with_path = false}]

(* e âˆˆ exp â©´ true | false | if(e){e}{e}
 *         | zero | succ(e) | pred(e) | iszero(e)
 *         | âŸ¨e,eâŸ© | projl(e) | projr(e)
 *         | x | let x â‰” e in e | Î»(x:Ï„).e | e(e)
 *         | Î›X.e | e[Ï„]
 *         | âŸ¨*Ï„,eâŸ© as âˆƒX.Ï„ | let âŸ¨*X,xâŸ© = e in e
 *)
type exp =
  | True
  | False
  | If of exp * exp * exp
  | Zero
  | Succ of exp
  | Pred of exp
  | IsZero of exp
  | Pair of exp * exp
  | Projl of exp
  | Projr of exp
  | Var of var
  | Let of var * exp * exp
  | Lambda of var * ty * exp
  | Apply of exp * exp
  | BigLambda of tvar * exp
  | TyApply of exp * ty
  | Pack of ty * exp * tvar * ty
  | Unpack of tvar * var * exp * exp
[@@deriving show {with_path = false}]

(*********************
 * Syntax for values *
 *********************)

(* nv âˆˆ nval â©´ zero | succ(nv)
 *)
type natval =
  | VZero
  | VSucc of natval
[@@deriving show {with_path = false}]

(* v âˆˆ value â©´ true | false
 *           | n
 *           | âŸ¨v,vâŸ©
 *           | Î»(x:Ï„).e
 *           | Î›X.e
 *           | âŸ¨*Ï„,vâŸ© as âˆƒX.Ï„
 *)
type value =
  | VTrue
  | VFalse
  | VNat of natval
  | VPair of value * value
  | VLambda of var * ty * exp
  | VBigLambda of tvar * exp
  | VPack of ty * value * tvar * ty
[@@deriving show {with_path = false}]

(***********************************
 * Syntax for type system contexts *
 ***********************************)

(* Î“ âˆˆ tenv â‰” var â‡€ type
 *)
type tscope = string_set
[@@deriving show {with_path = false}]

(* S âˆˆ scope â‰” â„˜(tvar)
 *)
type tenv = ty string_map
[@@deriving show {with_path = false}]

(****************************
 * Free variables for types *
 ****************************)

(* FV âˆˆ type â†’ â„˜(tvar) 
 * tfree_vars Ï„ â‰¡ FV(Ï„)
 *)
let rec tfree_vars (t0 : ty) : string_set = match t0 with
  | Bool -> StringSet.empty
  | Nat -> StringSet.empty
  | Prod(t1,t2) -> StringSet.union (tfree_vars t1) (tfree_vars t2)
  | Fun(t1,t2) -> StringSet.union (tfree_vars t1) (tfree_vars t2)
  | TVar(xt) -> StringSet.of_list [xt]
  | Forall(xt,t) -> StringSet.remove xt (tfree_vars t)
  | Exists(xt,t) -> StringSet.remove xt (tfree_vars t)

(**************************
 * Substitution for types *
 **************************)

(* An auxiliary function:
 * 
 *   trename X Xâ€² Ï„
 *
 * for renaming X to Xâ€² in type Ï„, i.e., [Xâ†¦Xâ€²]Ï„
 *)
let rec trename (xt : tvar) (xt' : tvar) (t0 : ty) : ty = match t0 with
  | Bool -> Bool
  | Nat -> Nat
  | Prod(t1,t2) -> Prod(trename xt xt' t1,trename xt xt' t2)
  | Fun(t1,t2) -> Fun(trename xt xt' t1,trename xt xt' t2)
  | TVar(yt) -> if xt = yt then TVar(xt') else TVar(yt)
  | Forall(yt,t) -> if xt = yt then Forall(yt,t) else Forall(yt,trename xt xt' t)
  | Exists(yt,t) -> if xt = yt then Exists(yt,t) else Forall(yt,trename xt xt' t)

(* An auxiliary function:
 *
 *   tfresh X Ï„â€² Ï„ = âŸ¨Xâ€²,Ï„â€³âŸ©
 *
 * which returns Xâ€² and Ï„â€³ such that
 *
 *   Xâ€² âˆ‰ FV(Ï„â€²) âˆª (FV(Ï„) âˆ– â´Xâµ)
 *   Ï„â€³ = [Xâ†¦Xâ€²]Ï„
 *
 * we first define an iterator helper function:
 *
 *   tfresh_i X Xâ€² Ï„â€² Ï„
 *
 * which tries the current candidate Xâ€², and iterates to try Xâ€² with a prime
 * appended to the variable symbol if Xâ€² isn't satisfactory.
 *
 * a call to
 *
 *   tfresh X Ï„â€² Ï„ 
 *
 * runs the iterator with X as the current guess.
 *)
let rec tfresh_i (xt : tvar) (xt' : tvar) (t' : ty) (t : ty) : tvar * ty =
  if StringSet.mem xt' (StringSet.union (tfree_vars t') (tfree_vars (Forall(xt,t))))
  then tfresh_i xt (xt' ^ "'") t' t
  else (xt',trename xt xt' t)

let tfresh (xt : tvar) (t' : ty) (t : ty) : tvar * ty = tfresh_i xt xt t' t

(* The capture-avoiding substitution function for types.
 *
 * [_â†¦_]_ âˆˆ tvar Ã— type Ã— type â†’ type
 * tsubst X Ï„â€² Ï„ â‰¡ [Xâ†¦Ï„â€²]Ï„
 *)
let rec tsubst (xt : tvar) (t' : ty) (t0 : ty) = match t0 with
  | Bool -> Bool
  | Nat -> Nat
  | Prod(t1,t2) -> Prod(tsubst xt t' t1,tsubst xt t' t2)
  | Fun(t1,t2) -> Fun(tsubst xt t' t1,tsubst xt t' t2)
  | TVar(yt) -> if xt = yt then t' else TVar(yt)
  | Forall(yt,t) -> 
      if xt = yt then Forall(xt,t) else 
      let (yt'',t'') = tfresh yt t' t in
      Forall(yt'',tsubst xt t' t'')
  | Exists(yt,t) ->
      if xt = yt then Exists(xt,t) else 
      let (yt'',t'') = tfresh yt t' t in
      Exists(yt'',tsubst xt t' t'')

(**********************************
 * Free variables for expressions *
 **********************************)

(* FV âˆˆ exp â†’ â„˜(var)
 * efree_vars e â‰¡ FV(e)
 *)
let rec efree_vars (e0 : exp) : string_set = match e0 with
  | True -> StringSet.empty
  | False -> StringSet.empty
  | If(e1,e2,e3) -> 
      StringSet.union 
        (efree_vars e1) 
        (StringSet.union (efree_vars e2) (efree_vars e3))
  | Zero -> StringSet.empty
  | Succ(e) -> efree_vars e
  | Pred(e) -> efree_vars e
  | IsZero(e) -> efree_vars e
  | Pair(e1,e2) -> StringSet.union (efree_vars e1) (efree_vars e2)
  | Projl(e) -> efree_vars e
  | Projr(e) -> efree_vars e
  | Var(x) -> StringSet.of_list [x]
  | Let(x,e1,e2) -> 
      StringSet.union 
        (efree_vars e1)
        (StringSet.remove x (efree_vars e2))
  | Lambda(x,t,e) ->
      StringSet.remove x (efree_vars e)
  | Apply(e1,e2) -> StringSet.union (efree_vars e1) (efree_vars e2)
  | BigLambda(xt,e) -> efree_vars e
  | TyApply(e,t) -> efree_vars e
  | Pack(t1,e,xt,t2) -> efree_vars e
  | Unpack(xt,x,e1,e2) -> 
      StringSet.union 
        (efree_vars e1)
        (StringSet.remove x (efree_vars e2))

(***********************************************
 * Substitution for expressions in expressions *
 ***********************************************)

(* Non-capture-avoiding substitution for expressions in expressions. Because
 * this is non-capture-avoiding, it assumes that the expression being
 * substituted is closed.
 *
 *   esubst_e_i x eâ€² e
 *
 * Assumption: eâ€² is closed 
 *
 * Do not use this function directly. Instead, use esubst_e which checks the
 * invariant.
 *)
let rec esubst_e_i (x : var) (e' : exp) (e0 : exp) : exp = match e0 with
  | True -> True
  | False -> False
  | If(e1,e2,e3) -> If(esubst_e_i x e' e1,esubst_e_i x e' e2,esubst_e_i x e' e3)
  | Zero -> Zero
  | Succ(e) -> Succ(esubst_e_i x e' e)
  | Pred(e) -> Pred(esubst_e_i x e' e)
  | IsZero(e) -> IsZero(esubst_e_i x e' e)
  | Pair(e1,e2) -> Pair(esubst_e_i x e' e1,esubst_e_i x e' e2)
  | Projl(e) -> Projl(esubst_e_i x e' e)
  | Projr(e) -> Projr(esubst_e_i x e' e)
  | Var(y) -> if x = y then e' else Var(y)
  | Let(y,e1,e2) -> 
      if x = y 
      then Let(x,esubst_e_i x e' e1,e2)
      else Let(y,esubst_e_i x e' e1,esubst_e_i x e' e2)
  | Lambda(y,t,e) ->
      if x = y
      then Lambda(x,t,e)
      else Lambda(y,t,esubst_e_i x e' e)
  | Apply(e1,e2) -> Apply(esubst_e_i x e' e1,esubst_e_i x e' e2)
  | BigLambda(xt,e) -> BigLambda(xt,esubst_e_i x e' e)
  | TyApply(e,t) -> TyApply(esubst_e_i x e' e,t)
  | Pack(t1,e,xt,t2) -> Pack(t1,esubst_e_i x e' e,xt,t2)
  | Unpack(xt,y,e1,e2) -> 
      if x = y
      then Unpack(xt,x,esubst_e_i x e' e1,e2)
      else Unpack(xt,y,esubst_e_i x e' e1,esubst_e_i x e' e2)

exception NOT_CLOSED_ERROR

(* A version of non-capture-avoiding substitution that raises an exception if
 * its required assumptions are not satisfied.
 *
 * [_â†¦_]_ âˆˆ var Ã— exp Ã— exp â†’ exp
 * esubst_e x eâ€² e â‰¡ [xâ†¦eâ€²]e
 *
 * Raises exception if eâ€² is not closed
 *)
let esubst_e (x : var) (e' : exp) (e : exp) : exp =
  if StringSet.equal (efree_vars e') StringSet.empty
  then esubst_e_i x e' e
  else raise NOT_CLOSED_ERROR

(*****************************************
 * Substitution for types in expressions *
 *****************************************)

(* Non-capture-avoiding substitution for types in expressions. Because this is
 * non-capture-avoiding, it assumes that the type being substituted is closed.
 *
 *   esubst_t_i X Ï„â€² e
 *
 * Assumption: Ï„â€² is closed 
 *
 * Do not use this function directly. Instead, use esubst_t which checks the
 * invariant.
 *)
let rec esubst_t_i (xt : tvar) (t' : ty) (e0 : exp) : exp = match e0 with
  | True -> True
  | False -> False
  | If(e1,e2,e3) -> If(esubst_t_i xt t' e1,esubst_t_i xt t' e2,esubst_t_i xt t' e3)
  | Zero -> Zero
  | Succ(e) -> Succ(esubst_t_i xt t' e)
  | Pred(e) -> Pred(esubst_t_i xt t' e)
  | IsZero(e) -> IsZero(esubst_t_i xt t' e)
  | Pair(e1,e2) -> Pair(esubst_t_i xt t' e1,esubst_t_i xt t' e2)
  | Projl(e) -> Projl(esubst_t_i xt t' e)
  | Projr(e) -> Projr(esubst_t_i xt t' e)
  | Var(x) -> Var(x)
  | Let(x,e1,e2) -> Let(x,esubst_t_i xt t' e1,esubst_t_i xt t' e2)
  | Lambda(x,t,e) -> Lambda(x,tsubst xt t' t,esubst_t_i xt t' e)
  | Apply(e1,e2) -> Apply(esubst_t_i xt t' e1,esubst_t_i xt t' e2)
  | BigLambda(yt,e) -> 
      if xt = yt
      then BigLambda(xt,e)
      else BigLambda(yt,esubst_t_i xt t' e)
  | TyApply(e,t) -> TyApply(esubst_t_i xt t' e,tsubst xt t' t)
  | Pack(t1,e,yt,t2) -> 
      if xt = yt
      then Pack(tsubst xt t' t1,esubst_t_i xt t' e,xt,t2)
      else Pack(tsubst xt t' t1,esubst_t_i xt t' e,yt,tsubst xt t' t2)
  | Unpack(yt,x,e1,e2) -> 
      if xt = yt
      then Unpack(xt,x,esubst_t_i xt t' e1,e2)
      else Unpack(xt,x,esubst_t_i xt t' e1,esubst_t_i xt t' e2)

(* A version of non-capture-avoiding substitution that raises an exception if
 * its required assumptions are not satisfied.
 *
 * [_â†¦_]_ âˆˆ tvar Ã— type Ã— exp â†’ exp
 * esubst_t X Ï„â€² e â‰¡ [Xâ†¦Ï„â€²]e
 *
 * Raises exception if Ï„â€² is not closed
 *)
let esubst_t (x : var) (t' : ty) (e : exp) : exp =
  if StringSet.equal (tfree_vars t') StringSet.empty
  then esubst_t_i x t' e
  else raise NOT_CLOSED_ERROR

(**********************************
 * Small step transition relation *
 **********************************)

(* Converting natval to exp *)
let rec exp_of_natval (nv0 : natval) : exp = match nv0 with
  | VZero -> Zero
  | VSucc(nv) -> Succ(exp_of_natval nv)

(* Converting val to exp *)
let rec exp_of_val (v0 : value) : exp = match v0 with
  | VTrue -> True
  | VFalse -> False
  | VNat(nv) -> exp_of_natval nv
  | VPair(v1,v2) -> Pair(exp_of_val v1,exp_of_val v2)
  | VLambda(x,t,e) -> Lambda(x,t,e)
  | VBigLambda(xt,e) -> BigLambda(xt,e)
  | VPack(t1,v,xt,t2) -> Pack(t1,exp_of_val v,xt,t2)

(* A result is either a value, an expression, or the symbol `stuck`.
 *
 * r âˆˆ result â©´ v | e | stuck
 *)
type result =
  | Val of value
  | Step of exp
  | Stuck
[@@deriving show {with_path = false}]


(* The small-step relation e â€”â†’ e
 *
 * Assumption: e is closed.
 *
 * If step(e) = v, then e is a value (and does not take a step).
 * (i.e., e âˆˆ val)
 *
 * If step(e) = eâ€², then e steps to eâ€².
 * (i.e., e â€”â†’ eâ€²)
 *
 * If step(e) = stuck, then e is stuck, that is e is not a value and does not
 * take a step.
 * (i.e., e âˆ‰ val and e â€”â†›)
 *)
let rec step (e0 : exp) : result = match e0 with
  (* true âˆˆ val *)
  | True -> Val(VTrue)
  (* false âˆˆ val *)
  | False -> Val(VFalse)
  | If(e1,e2,e3) -> begin match step e1 with
      (* [If-True] 
       * if(true){eâ‚‚}{eâ‚ƒ} â€”â†’ eâ‚‚ *)
      | Val(VTrue) -> Step(e2)
      (* [If-False] 
       * if(false){eâ‚‚}{eâ‚ƒ} â€”â†’ eâ‚ƒ *)
      | Val(VFalse) -> Step(e3)
      (* v âˆ‰ {true,false} 
       * âŸ¹  
       * if(v){eâ‚‚}{eâ‚ƒ} âˆ‰ val
       * if(v){eâ‚‚}{eâ‚ƒ} â€”â†› *)
      | Val(_) -> Stuck
      (* [If-Cong] 
       * eâ‚ â€”â†’ eâ‚â€² 
       * âŸ¹  
       * if(eâ‚){eâ‚‚}{eâ‚ƒ} â€”â†’ if(eâ‚â€²){eâ‚‚}{eâ‚ƒ} *)
      | Step(e1') -> Step(If(e1',e2,e3))
      (* eâ‚ âˆ‰ val
       * eâ‚ â€”â†›
       * âŸ¹ 
       * if(eâ‚){eâ‚‚}{eâ‚ƒ} âˆ‰ val
       * if(eâ‚){eâ‚‚}{eâ‚ƒ} â€”â†› *)
      | Stuck -> Stuck
      end
  (* zero âˆˆ val *)
  | Zero -> Val(VNat(VZero))
  | Succ(e) -> begin match step e with
      (* nv âˆˆ nval
       * âŸ¹
       * succ(nv) âˆˆ nval âŠ† val *)
      | Val(VNat(nv)) -> Val(VNat(VSucc(nv)))
      (* v âˆ‰ nval
       * âŸ¹
       * succ(v) âˆ‰ val
       * succ(v) â€”â†› *)
      | Val(_) -> Stuck
      (* [Succ-Cong]
       * e â€”â†’ eâ€²
       * succ(e) â€”â†’ succ(eâ€²) *)
      | Step(e') -> Step(Succ(e'))
      (* e âˆ‰ val
       * e â€”â†›
       * âŸ¹
       * succ(e) âˆ‰ val
       * succ(e) â€”â†› *)
      | Stuck -> Stuck
      end
  | Pred(e) -> begin match step e with
      (* [Pred-Zero]
       * pred(zero) â€”â†’ zero *)
      | Val(VNat(VZero)) -> Step(Zero)
      (* [Pred-Succ]
       * pred(succ(nv)) â€”â†’ nv *)
      | Val(VNat(VSucc(nv))) -> Step(exp_of_natval nv)
      (* v âˆ‰ nval
       * âŸ¹
       * pred(v) âˆ‰ val
       * pred(v) â€”â†› *)
      | Val(_) -> Stuck
      (* [Pred-Cong]
       * e â€”â†’ eâ€²
       * âŸ¹
       * pred(e) â€”â†’ pred(eâ€²) *)
      | Step(e') -> Step(Pred(e'))
      (* e âˆ‰ val
       * e â€”â†›
       * âŸ¹
       * pred(e) âˆ‰ val
       * pred(e) â€”â†› *)
      | Stuck -> Stuck
      end
  | IsZero(e) -> begin match step e with
      (* [IsZero-Zero]
       * iszero(zero) â€”â†’ true *)
      | Val(VNat(VZero)) -> Step(True)
      (* [IsZero-Succ]
       * iszero(succ(nv)) â€”â†’ false *)
      | Val(VNat(VSucc(nv))) -> Step(False)
      (* v âˆ‰ nval
       * âŸ¹
       * iszero(v) âˆ‰ val
       * iszero(v) â€”â†› *)
      | Val(_) -> Stuck
      (* [IsZero-Cong]
       * e â€”â†’ eâ€²
       * âŸ¹
       * iszero(e) â€”â†’ iszero(eâ€²) *)
      | Step(e') -> Step(IsZero(e'))
      (* e âˆ‰ val
       * e â€”â†›
       * âŸ¹
       * iszero(e) âˆ‰ val
       * iszero(e) â€”â†› *)
      | Stuck -> Stuck
      end
  | Pair(e1,e2) -> begin match step e1 with
      | Val(v1) -> begin match step e2 with
          (* âŸ¨vâ‚,vâ‚‚âŸ© âˆˆ val *)
          | Val(v2) -> Val(VPair(v1,v2))
          (* [Pair-Cong-2]
           * e â€”â†’ eâ€²
           * âŸ¹
           * âŸ¨v,eâŸ© â€”â†’ âŸ¨v,eâ€²âŸ© *)
          | Step(e2') -> Step(Pair(e1,e2'))
          (* e âˆ‰ val
           * e â€”â†›
           * âŸ¹
           * âŸ¨v,eâŸ© âˆ‰ val
           * âŸ¨v,eâŸ© â€”â†› *)
          | Stuck -> Stuck
          end
      (* [Pair-Cong-1]
       * eâ‚ â€”â†’ eâ‚â€²
       * âŸ¹
       * âŸ¨eâ‚,eâ‚‚âŸ© â€”â†’ âŸ¨eâ‚â€²,eâ‚‚âŸ© *)
      | Step(e1') -> Step(Pair(e1',e2))
      (* eâ‚ âˆ‰ val
       * eâ‚ â€”â†›
       * âŸ¹
       * âŸ¨eâ‚,eâ‚‚âŸ© âˆ‰ val
       * âŸ¨eâ‚,eâ‚‚âŸ© â€”â†› *)
      | Stuck -> Stuck
      end
  | Projl(e1) -> begin match step e1 with
      (* [Projl-Pair]
       * projl(âŸ¨vâ‚,vâ‚‚âŸ©) â€”â†’ vâ‚ *)
      | Val(VPair(v1,v2)) -> Step(exp_of_val v1)
      (* âˆ„vâ‚,vâ‚‚. v = âŸ¨vâ‚,vâ‚‚âŸ©
       * âŸ¹
       * projl(v) âˆ‰ val
       * projl(v) â€”â†› *)
      | Val(_) -> Stuck
      (* [Projl-Cong]
       * e â€”â†’ eâ€²
       * âŸ¹
       * projl(e) â€”â†’ projl(eâ€²) *)
      | Step(e1') -> Step(Projl(e1'))
      (* e âˆ‰ val
       * e â€”â†›
       * âŸ¹
       * projl(e) âˆ‰ val
       * projl(e) â€”â†› *)
      | Stuck -> Stuck
      end
  | Projr(e1) -> begin match step e1 with
      (* [Projr-Pair]
       * projr(âŸ¨vâ‚,vâ‚‚âŸ©) â€”â†’ vâ‚‚ *)
      | Val(VPair(v1,v2)) -> Step(exp_of_val v2)
      (* âˆ„vâ‚,vâ‚‚. v = âŸ¨vâ‚,vâ‚‚âŸ©
       * âŸ¹
       * projr(v) âˆ‰ val
       * projr(v) â€”â†› *)
      | Val(_) -> Stuck
      (* [Projr-Cong]
       * e â€”â†’ eâ€²
       * âŸ¹
       * projr(e) â€”â†’ projr(eâ€²) *)
      | Step(e1') -> Step(Projr(e1'))
      (* e âˆ‰ val
       * e â€”â†›
       * âŸ¹
       * projr(e) âˆ‰ val
       * projr(e) â€”â†› *)
      | Stuck -> Stuck
      end
  (* x is not closed *)
  | Var(x) -> raise NOT_CLOSED_ERROR
  | Let(x,e1,e2) -> begin match step e1 with                                              
      (* [Let-Val]                                                                      
       * let x â‰” v in e â€”â†’ [xâ†¦v]e *)                                                      
      | Val(v1) -> Step(esubst_e x (exp_of_val v1) e2)                                  
      (* [Let-Cong]                                                                       
       * eâ‚ â€”â†’ eâ‚â€²                                                                      
       * âŸ¹                                                                                
       * let x â‰” eâ‚ in eâ‚‚ â€”â†’ let x = eâ‚â€² in eâ‚‚ *)                                       
      | Step(e1') -> Step(Let(x,e1',e2))                                                  
      (* eâ‚ âˆ‰ val                                                                       
       * eâ‚ â€”â†›                                                                            
       * âŸ¹                                                                              
       * let x â‰” eâ‚ in eâ‚‚ âˆ‰ val                                                           
       * let x â‰” eâ‚ in eâ‚‚ â€”â†› *)                                                         
      | Stuck -> Stuck                                                                    
      end                                                                               
  (* Î»(x:Ï„).e âˆˆ val *)                                                                    
  | Lambda(x,t,e) -> Val(VLambda(x,t,e))                                                
  | Apply(e1,e2) -> begin match step e1 with                                              
      | Val(v1) -> begin match step e2 with                                             
          | Val(v2) -> begin match v1 with                                                
              (* [Apply-Lambda (Î²)]                                                     
               * (Î»(x:Ï„).e)v â€”â†’ [xâ†¦v]e *)                                                 
              | VLambda(x,t,e) -> Step(esubst_e x (exp_of_val v2) e)                    
              (* âˆ„x,Ï„,e. vâ‚ = Î»(x:Ï„).e                                                    
               * âŸ¹                                                                      
               * vâ‚(vâ‚‚) âˆ‰ val                                                             
               * vâ‚(vâ‚‚) â€”â†› *)                                                           
              | _ -> Stuck                                                                
              end                                                                       
          (* [Apply-Cong-2]                                                               
           * e â€”â†’ eâ€²                                                                    
           * âŸ¹                                                                            
           * v(e) â€”â†’ v(eâ€²) *)                                                           
          | Step(e2') -> Step(Apply(e1,e2'))                                              
          (* e âˆ‰ val                                                                    
           * e â€”â†›                                                                         
           * âŸ¹                                                                          
           * v(e) âˆ‰ val                                                                   
           * v(e) â€”â†› *)                                                                 
          | Stuck -> Stuck                                                                
          end                                                                           
      (* [Apply-Cong-1]                                                                   
       * eâ‚ â€”â†’ eâ‚â€²                                                                      
       * âŸ¹                                                                                
       * eâ‚(eâ‚‚) â€”â†’ eâ‚â€²(eâ‚‚) *)                                                           
      | Step(e1') -> Step(Apply(e1',e2))                                                  
      (* eâ‚ âˆ‰ val                                                                       
       * eâ‚ â€”â†›                                                                            
       * âŸ¹                                                                              
       * eâ‚(eâ‚‚) âˆ‰ val                                                                     
       * eâ‚(eâ‚‚) â€“â†› *)                                                                   
      | Stuck -> Stuck                                                                    
      end                                                                               
  (* Î›X.e âˆˆ val *)                                                                        
  | BigLambda(xt,e) -> Val(VBigLambda(xt,e))                                            
  | TyApply(e,t) -> begin match step e with                                               
      (* [Type-Apply-Lambda]                                                            
       * (Î›X.e)[Ï„] â€”â†’ [Xâ†¦Ï„]e *)                                                           
      | Val(VBigLambda(xt,e')) -> Step(esubst_t xt t e')                                
      (* âˆ„X,e. v = Î›X.e                                                                   
       * âŸ¹                                                                              
       * v[Ï„] âˆ‰ val                                                                       
       * v[Ï„] â€”â†› *)                                                                     
      | Val(_) -> Stuck                                                                   
      (* [Type-Apply-Cong]                                                              
       * e â€”â†’ eâ€²                                                                          
       * âŸ¹                                                                              
       * e[Ï„] â€”â†’ eâ€²[Ï„] *)                                                                 
      | Step(e') -> Step(TyApply(e',t))                                                 
      (* e âˆ‰ val                                                                          
       * e â€”â†›                                                                           
       * âŸ¹                                                                                
       * e[Ï„] âˆ‰ val                                                                     
       * e[Ï„] â€”â†› val *)                                                                   
      | Stuck -> Stuck                                                                  
      end                                                                                 
  | Pack(t1,e,xt,t2) -> begin match step e with                                         
      (* âŸ¨*Ï„,vâŸ© as âˆƒX.Ï„ âˆˆ val *)                                                          
      | Val(v) -> Val(VPack(t1,v,xt,t2))                                                
      (* [Pack-Cong]                                                                      
       * e â€”â†’ eâ€²                                                                        
       * âŸ¹                                                                                
       * âŸ¨*Ï„,eâŸ© as âˆƒX.Ï„ â€”â†’ âŸ¨*Ï„,eâ€²âŸ© as âˆƒX.Ï„ *)                                           
      | Step(e') -> Step(Pack(t1,e',xt,t2))                                               
      (* e âˆ‰ val                                                                        
       * e â€”â†›                                                                             
       * âŸ¹                                                                              
       * âŸ¨*Ï„,eâŸ© as âˆƒX.Ï„ âˆ‰ val                                                             
       * âŸ¨*Ï„,eâŸ© as âˆƒX.Ï„ â€”â†› *)                                                           
      | Stuck -> Stuck                                                                    
      end                                                                               
  | Unpack(xt,x,e1,e2) -> begin match step e1 with                                        
      (* [Unpack-Pack]                                                                  
       * let âŸ¨*X,xâŸ© â‰” âŸ¨*Ï„â€²,vâŸ© as âˆƒX.Ï„ in e â€”â†’ [Xâ†¦Ï„â€²][xâ†¦v]e *)                             
      | Val(VPack(t1,v,yt,t2)) -> Step(esubst_t xt t1 (esubst_e x (exp_of_val v) e2))   
      (* âˆ„ Ï„â€²,vâ€²,X,Ï„. v = âŸ¨*Ï„â€²,vâ€²âŸ© as âˆƒX.Ï„                                                
       * âŸ¹                                                                              
       * let âŸ¨*X,xâŸ© â‰” v in e âˆ‰ val                                                        
       * let âŸ¨*X,xâŸ© â‰” v in e â€”â†› *)                                                      
      | Val(_) -> Stuck                                                                   
      (* [Unpack-Cong]                                                                  
       * eâ‚ â€”â†’ eâ‚â€²                                                                        
       * let âŸ¨*X,xâŸ© â‰” eâ‚ in eâ‚‚ â€”â†’ let âŸ¨*X,xâŸ© â‰” eâ‚â€² in eâ‚‚ *)                             
      | Step(e1') -> Step(Unpack(xt,x,e1',e2))                                            
      (* eâ‚ âˆ‰ val                                                                       
       * eâ‚ â€”â†›                                                                            
       * âŸ¹                                                                              
       * let âŸ¨*X,xâŸ© â‰” eâ‚ in eâ‚‚ âˆ‰ val                                                      
       * let âŸ¨*X,xâŸ© â‰” eâ‚ in eâ‚‚ â€”â†› *)                                                    
      | Stuck -> Stuck                                                                    
      end                                                                               

(* The reflexive transitive closure of the small-step relation e â€”â†’* e *)
let rec step_star (e : exp) : exp = match step e with
  | Val(v) -> exp_of_val v
  | Step(e') -> step_star e'
  | Stuck -> e

(**********************
 * Well-scoped relation
 **********************)

(* The relation:
 *
 *   S âŠ¢ Ï„
 *
 * scope_ok S Ï„ = true âŸº  S âŠ¢ Ï„
 *)
let rec scope_ok (s : tscope) (t0 : ty) : bool = match t0 with   
  (* [Bool]                                                        
   * S âŠ¢ bool *)                                                 
  | Bool -> true                                                   
  (* [Nat]                                                       
   * S âŠ¢ nat *)                                                    
  | Nat -> true                                                  
  (* [Prod]                                                        
   * S âŠ¢ Ï„â‚                                                      
   * S âŠ¢ Ï„â‚‚                                                        
   * âŸ¹                                                           
   * S âŠ¢ Ï„â‚ Ã— Ï„â‚‚ *)                                                
  | Prod(t1,t2) -> scope_ok s t1 && scope_ok s t2                
  (* [Fun]                                                         
   * S âŠ¢ Ï„â‚                                                      
   * S âŠ¢ Ï„â‚‚                                                        
   * âŸ¹                                                           
   * S âŠ¢ Ï„â‚ â†’ Ï„â‚‚ *)                                                
  | Fun(t1,t2) -> scope_ok s t1 && scope_ok s t2                 
  (* [TVar]                                                        
   * X âˆˆ S                                                       
   * âŸ¹                                                             
   * S âŠ¢ X *)                                                    
  | TVar(xt) -> StringSet.mem xt s                                 
  (* [Forall]                                                    
   * Sâˆª{X} âŠ¢ Ï„                                                     
   * âŸ¹                                                           
   * S âŠ¢ âˆ€X.Ï„ *)                                                   
  | Forall(xt,t) -> scope_ok (StringSet.add xt s) t              
  (* [Exists]                                                      
   * Sâˆª{X} âŠ¢ Ï„                                                   
   * âŸ¹                                                             
   * S âŠ¢ âˆƒX.Ï„ *)                                                 
  | Exists(xt,t) -> scope_ok (StringSet.add xt s) t                

(***********************
 * Well-typed relation *
 ***********************)

(* An auxiliary function for testing the equality of two types, modulo alpha
 * conversion.
 *
 * First, a helper function tequal_r which keeps track of which bindings are
 * equal by assigning them to unique numbers.
 *)
let rec tequal_r (l : int) (t1e : int string_map) (t2e : int string_map) (t1 : ty) (t2 : ty) : bool = match t1 , t2 with
  | Bool , Bool -> true
  | Nat , Nat -> true
  | Fun(t11,t12) , Fun(t21,t22) -> tequal_r l t1e t2e t11 t21 && tequal_r l t1e t2e t12 t22
  | Prod(t11,t12) , Prod(t21,t22) -> tequal_r l t1e t2e t11 t21 && tequal_r l t1e t2e t12 t22
  | TVar(x) , TVar(y) -> 
      if StringMap.mem x t1e && StringMap.mem y t2e
      then StringMap.find x t1e = StringMap.find y t2e
      else x = y
  | Forall(xt,t1) , Forall(yt,t2) -> tequal_r (l+1) (StringMap.add xt l t1e) (StringMap.add yt l t2e) t1 t2
  | Forall(_) , _ -> false | _ , Forall(_) -> false
  | Exists(xt,t1) , Exists(yt,t2) -> tequal_r (l+1) (StringMap.add xt l t1e) (StringMap.add yt l t2e) t1 t2
  | _ , _ -> false

(* tequal Ï„â‚ Ï„â‚‚ = true âŸº  Ï„â‚ â‰ˆáµ… Ï„â‚‚ 
 *
 * !! use tequal in your implementation of infer anytime you need to compare
 * two types for equality 
 *)
let tequal (t1 : ty) (t2 : ty) : bool = tequal_r 1 StringMap.empty StringMap.empty t1 t2

exception TYPE_ERROR

(* The relation:
 *
 *   S , Î“ âŠ¢ e : Ï„
 *
 * infer S Î“ e = Ï„ âŸº  S , Î“ âŠ¢ : Ï„
 *)
let rec infer (s : tscope) (g : tenv) (e0 : exp) : ty = match e0 with
  (* [True]
   * S , Î“ âŠ¢ true : bool *)
  | True -> Bool
  (* [False]
   * S , Î“ âŠ¢ false : bool *)
  | False -> Bool
  (* [If]
   * S , Î“ âŠ¢ eâ‚ : bool
   * S , Î“ âŠ¢ eâ‚‚ : Ï„
   * S , Î“ âŠ¢ eâ‚ƒ : Ï„
   * âŸ¹
   * S , Î“ âŠ¢ if(eâ‚){eâ‚‚}{eâ‚ƒ} : Ï„ *)
  | If(e1,e2,e3) ->
      let t1 = infer s g e1 in
      let t2 = infer s g e2 in
      let t3 = infer s g e3 in
      if not (t1 = Bool) then raise TYPE_ERROR else
      if not (t2 = t3) then raise TYPE_ERROR else
      t2
  (* [Zero]
   * S , Î“ âŠ¢ zero : nat *)
  | Zero -> Nat
  (* [Succ]
   * S , Î“ âŠ¢ e : nat
   * âŸ¹
   * S , Î“ âŠ¢ succ(e) : nat *)
  | Succ(e) ->
      let t = infer s g e in
      if not (t = Nat) then raise TYPE_ERROR else
      Nat
  (* [Pred]
   * S , Î“ âŠ¢ e : nat
   * âŸ¹
   * S , Î“ âŠ¢ pred(e) : nat *)
  | Pred(e) ->
      let t = infer s g e in
      if not (t = Nat) then raise TYPE_ERROR else
      Nat
  (* [IsZero]
   * S , Î“ âŠ¢ e : nat
   * âŸ¹
   * S , Î“ âŠ¢ iszero(e) : bool *)
  | IsZero(e) ->
      let t = infer s g e in
      if not (t = Nat) then raise TYPE_ERROR else
      Bool
  (* [Pair]
   * S , Î“ âŠ¢ eâ‚ : Ï„â‚
   * S , Î“ âŠ¢ eâ‚‚ : Ï„â‚‚
   * âŸ¹
   * S , Î“ âŠ¢ âŸ¨eâ‚,eâ‚‚âŸ© : Ï„â‚ Ã— Ï„â‚‚ *)
  | Pair(e1,e2) ->
      let t1 = infer s g e1 in
      let t2 = infer s g e2 in
      Prod(t1,t2)
  (* [Projl]
   * S , Î“ âŠ¢ e : Ï„â‚ Ã— Ï„â‚‚
   * âŸ¹
   * S , Î“ âŠ¢ projl(e) : Ï„â‚ *)
  | Projl(e) ->
      let t = infer s g e in
      begin match t with
      | Prod(t1,_) -> t1
      | _ -> raise TYPE_ERROR
      end
  (* [Projr]
   * S , Î“ âŠ¢ e : Ï„â‚ Ã— Ï„â‚‚
   * âŸ¹
   * S , Î“ âŠ¢ projr(e) : Ï„â‚‚ *)
  | Projr(e) ->
      let t = infer s g e in
      begin match t with
      | Prod(_,t2) -> t2
      | _ -> raise TYPE_ERROR
      end
  (* [Var]
   * Î“(x) = Ï„
   * âŸ¹
   * S , Î“ âŠ¢ x : Ï„ *)
  | Var(x) -> StringMap.find x g                                       
  (* [Var]                                                               
   * S , Î“ âŠ¢ eâ‚ : Ï„â‚                                                   
   * S , Î“[xâ†¦Ï„â‚] âŠ¢ eâ‚‚ : Ï„â‚‚                                               
   * âŸ¹                                                                 
   * S , Î“ âŠ¢ let x â‰” eâ‚ in eâ‚‚ : Ï„â‚‚ *)                                    
  | Let(x,e1,e2) ->                                                    
      let t = infer s g e1 in                                            
      infer s (StringMap.add x t g) e2                                 
  (* [Lambda]                                                            
   * S âŠ¢ Ï„â‚                                                            
   * S , Î“[xâ†¦Ï„â‚] âŠ¢ e : Ï„â‚‚                                                
   * âŸ¹                                                                 
   * S , Î“ âŠ¢ Î»(x:Ï„â‚).e : Ï„â‚ â†’ Ï„â‚‚ *)                                      
  | Lambda(x,t1,e) ->                                                  
      if not (scope_ok s t1) then raise TYPE_ERROR else                  
      let t2 = infer s (StringMap.add x t1 g) e in                     
      Fun(t1,t2)                                                         
  (* [Apply]                                                           
   * S , Î“ âŠ¢ eâ‚ : Ï„â‚ â†’ Ï„â‚‚                                                
   * S , Î“ âŠ¢ eâ‚‚ : Ï„â‚                                                   
   * âŸ¹                                                                   
   * S , Î“ âŠ¢ eâ‚(eâ‚‚) : Ï„â‚‚ *)                                            
  | Apply(e1,e2) ->                                                      
      let t1 = infer s g e1 in                                         
      let t2 = infer s g e2 in                                           
      begin match t1 with                                              
      | Fun(t11,t12) ->                                                  
          if not (t11 = t2) then raise TYPE_ERROR else                 
          t12                                                            
      | _ -> raise TYPE_ERROR                                          
      end                                                                
  (* [TypeLambda]                                                      
   * Sâˆª{X} , Î“ âŠ¢ e : Ï„                                                   
   * âŸ¹                                                                 
   * S , Î“ âŠ¢ Î›X.e : âˆ€X.Ï„ *)                                              
  | BigLambda(xt,e) ->                                                 
      let t = infer (StringSet.add xt s) g e                             
      in Forall(xt,t)                                                  
  (* [TypeApply]                                                         
   * S âŠ¢ Ï„â€²                                                            
   * S , Î“ âŠ¢ e : âˆ€X.Ï„                                                    
   * âŸ¹                                                                 
   * S , Î“ âŠ¢ e[Ï„â€²] : [Xâ†¦Ï„â€²]Ï„ *)                                          
  | TyApply(e,t') ->                                                   
      if not (scope_ok s t') then raise TYPE_ERROR else                  
      let t = infer s g e in                                           
      begin match t with                                                 
      | Forall(xt,t'') -> tsubst xt t' t''                             
      | _ -> raise TYPE_ERROR                                            
      end                                                              
  (* [Pack]                                                              
   * S , Î“ âŠ¢ e : [Xâ†¦Ï„â€²]Ï„                                               
   * âŸ¹                                                                   
   * S , Î“ âŠ¢ âŸ¨*Ï„â€²,eâŸ© as âˆƒ X.Ï„ : âˆƒX.Ï„ *)                                
  | Pack(t1,e,xt,t2) ->                                                  
      let t = infer s g e in                                           
      let t' = tsubst xt t1 t2 in                                        
      if not (tequal t t') then raise TYPE_ERROR else                  
      Exists(xt,t2)                                                      
  (* [Unpack-Alt]                                                      
   * S âŠ¢ Ï„â‚‚                                                              
   * S , Î“ âŠ¢ eâ‚ : âˆƒY.Ï„â‚                                                
   * Sâˆª{X} , Î“[xâ†¦[Yâ†¦X]Ï„â‚] âŠ¢ eâ‚‚ : Ï„â‚‚                                      
   * âŸ¹                                                                 
   * let âŸ¨*X,xâŸ© â‰” eâ‚ in eâ‚‚ : Ï„â‚‚ *)                                       
  | Unpack(xt,x,e1,e2) ->                                              
      let t1 = infer s g e1 in                                           
      begin match t1 with                                              
      | Exists(yt,t1') ->                                                
          let s' = StringSet.add xt s in                               
          let g' = StringMap.add x (tsubst yt (TVar(xt)) t1') g in       
          let t2 = infer s' g'  e2 in                                  
          if not (scope_ok s t2) then raise TYPE_ERROR else              
          t2                                                           
      | _ -> raise TYPE_ERROR                                            
      end                                                              

(***********
 * Testing *
 ***********)

type 'a test_result =
  | R of 'a
  | TypeError
  | NotClosedError
[@@deriving show {with_path = false}]

let step_test_result (e : exp) : result test_result =
  try
    R(step e)
  with
    | TYPE_ERROR -> TypeError
    | NOT_CLOSED_ERROR -> NotClosedError

let infer_test_result (e : exp) : ty test_result =
  try
    R(infer StringSet.empty StringMap.empty e)
  with
    | TYPE_ERROR -> TypeError
    | NOT_CLOSED_ERROR -> NotClosedError

let step_tests : test_block =
  let pid : exp = BigLambda("X",Lambda("x",TVar("X"),Var("x"))) in
  let pidid : exp = Lambda("x",Forall("X",Fun(TVar("X"),TVar("X"))),Var("x")) in
  let ppid : exp = BigLambda("X",pid) in
  let fiveo : exp = Pack(Nat,Pair(Zero,Lambda("x",Nat,Var("x"))),"X",Prod(TVar("X"),Fun(TVar("X"),TVar("X")))) in
  let unp : exp = Unpack("Y","y",fiveo,Pack(TVar("Y"),Apply(Projr(Var("y")),Projl(Var("y"))),"Z",TVar("Z"))) in
  TestBlock
  ( "STEP"
  , [ pid                           , R(Val(VBigLambda("X",Lambda("x",TVar("X"),Var("x")))))
    ; TyApply(pid,Nat)              , R(Step(Lambda("x",Nat,Var("x"))))
    ; TyApply(pid,Bool)             , R(Step(Lambda("x",Bool,Var("x"))))
    ; TyApply(pid,TVar("Y"))        , NotClosedError
    ; TyApply(Apply(pidid,pid),Nat) , R(Step(TyApply(pid,Nat)))
    ; TyApply(ppid,Nat)             , R(Step(pid))
    ; fiveo                         , R(Val(VPack(Nat,VPair(VNat(VZero),VLambda("x",Nat,Var("x"))),"X",Prod(TVar("X"),Fun(TVar("X"),TVar("X"))))))
    ; unp                           , R(Step(Pack(Nat,Apply(Projr(Pair(Zero,Lambda("x",Nat,Var("x")))),Projl(Pair(Zero,Lambda("x",Nat,Var("x"))))),"Z",TVar("Z"))))
    ]
  , step_test_result
  , (=)
  , [%show : exp]
  , [%show : result test_result]
  )

let infer_tests = 
  (* pid = Î›X.Î»(x:X).x 
   * pid : âˆ€X.Xâ†’X
   *)
  let pid : exp = BigLambda("X",Lambda("x",TVar("X"),Var("x"))) in
  let pidt : ty = Forall("X",Fun(TVar("X"),TVar("X"))) in
  (* pidid = Î»(x:âˆ€X.Xâ†’X).x
   * pidid = (âˆ€X.Xâ†’X) â†’ (âˆ€X.Xâ†’X)
   *)
  let pidid : exp = Lambda("x",pidt,Var("x")) in
  let pididt : ty = Fun(pidt,pidt) in
  (* ppid = Î›X.Î›X.Î»(x:X).x
   * ppid = âˆ€X.âˆ€X.Xâ†’X
   *)
  let ppid : exp = BigLambda("X",pid) in
  let ppidt : ty = Forall("X",pidt) in
  (* fiveo = âŸ¨*nat,âŸ¨0,(Î»(x:nat).x)âŸ©âŸ© as âˆƒX.XÃ—(Xâ†’X) 
   * fiveo : âˆƒX.XÃ—(Xâ†’X)
   *)
  let fiveo : exp = Pack(Nat,Pair(Zero,Lambda("x",Nat,Var("x"))),"X",Prod(TVar("X"),Fun(TVar("X"),TVar("X")))) in
  let fiveot = Exists("X",Prod(TVar("X"),Fun(TVar("X"),TVar("X")))) in
  (* fiveoâ€² = âŸ¨*nat,âŸ¨0,(Î»(x:nat).x)âŸ©âŸ© as âˆƒX.XÃ—(Xâ†’nat) 
   * fiveoâ€² : âˆƒX.XÃ—(Xâ†’X)
   *)
  let fiveo' : exp = Pack(Nat,Pair(Zero,Lambda("x",Nat,Var("x"))),"X",Prod(TVar("X"),Fun(TVar("X"),Nat))) in
  let fiveo't = Exists("X",Prod(TVar("X"),Fun(TVar("X"),Nat))) in
  (* fiveobad = âŸ¨*nat,âŸ¨0,(Î»(x:nat).x)âŸ©âŸ© as âˆƒX.XÃ—(Xâ†’bool) 
   *)
  let fiveobad : exp = Pack(Nat,Pair(Zero,Lambda("x",Nat,Var("x"))),"X",Prod(TVar("X"),Fun(TVar("X"),Bool))) in
  (* unp = let âŸ¨*Y,yâŸ© â‰” âŸ¨*nat,âŸ¨0,(Î»(x:nat).x)âŸ©âŸ© as âˆƒX.XÃ—(Xâ†’X) in âŸ¨*Y,projr(y)(projl(y))âŸ© as âˆƒZ.Z
   * unp : âˆƒZ.Z
   *)
  let unp : exp = Unpack("Y","y",fiveo,Pack(TVar("Y"),Apply(Projr(Var("y")),Projl(Var("y"))),"Z",TVar("Z"))) in
  let unpt : ty = Exists("Z",TVar("Z")) in
  (* unp' = let âŸ¨*Y,yâŸ© â‰” âŸ¨*nat,âŸ¨0,(Î»(x:nat).x)âŸ©âŸ© as âˆƒX.XÃ—(Xâ†’X) in âŸ¨*Y,pid[Y](projr(y)(projl(y)))âŸ© as âˆƒZ.Z
   * unp' : âˆƒZ.Z
   *)
  let unp' : exp = Unpack("Y","y",fiveo,Pack(TVar("Y"),Apply(TyApply(pid,TVar("Y")),Apply(Projr(Var("y")),Projl(Var("y")))),"Z",TVar("Z"))) in
  let unp't : ty = Exists("Z",TVar("Z")) in
  (* bunp = let âŸ¨*Y,yâŸ© â‰” âŸ¨*nat,âŸ¨0,(Î»(x:nat).x)âŸ©âŸ© as âˆƒX.XÃ—(Xâ†’X) in projr(y)(projl(y)) *)
  let bunp : exp = Unpack("Y","y",fiveo,Apply(Projr(Var("y")),Projl(Var("y")))) in
  (* pidpair = Î›X.Î›Y.âŸ¨(Î»(x:X).x),(Î»(x:Y).x)âŸ©
   * pidpair : âˆ€X.âˆ€Y.(Xâ†’X)Ã—(Yâ†’Y)
   *)
  let _pidpair : exp = BigLambda("X",BigLambda("Y",Pair(TyApply(pid,TVar("X")),TyApply(pid,TVar("Y"))))) in
  let _pidpairt : ty = Forall("X",Forall("Y",Prod(Fun(TVar("X"),TVar("X")),Fun(TVar("Y"),TVar("Y"))))) in
  (* weird1 = (Î›Y.pidpair)[nat]
   * weird1 : [Yâ†¦nat](âˆ€X.âˆ€Y.(Xâ†’X)Ã—(Yâ†’Y)
   * weird1 : pidpairt
   *)
  let _weird1 : exp = TyApply(BigLambda("Y",_pidpair),Nat) in
  (* weird2 = âˆ€Y.pidpair[Y]
   * weird2 : âˆ€Y.[Xâ†¦Y](âˆ€Y.(Xâ†’X)Ã—(Yâ†’Y))
   * weird2 : âˆ€Y.âˆ€Yâ€².(Yâ†’Y)Ã—(Yâ€²â†’Yâ€²)
   *)
  let _weird2 : exp = BigLambda("Y",TyApply(_pidpair,TVar("Y"))) in
  let _weird2t : ty = Forall("Y",Forall("Y'",Prod(Fun(TVar("Y"),TVar("Y")),Fun(TVar("Y'"),TVar("Y'"))))) in
  (* weird3 = Î›Yâ€².Î›Y.(Î›X.Î›Y.âŸ¨âŸ¨(Î»(x:Yâ€²).x),(Î»(x:X).x)âŸ©,(Î»(x:Y).x)âŸ©)[Y]
   * weird3 : âˆ€Yâ€².âˆ€Y.[Xâ†¦Y](âˆ€Y.(Yâ€²â†’Yâ€²)Ã—(Xâ†’X)Ã—(Yâ†’Y))
   * weird3 : âˆ€Yâ€².âˆ€Y.âˆ€Yâ€³.(Yâ€²â†’Yâ€²)Ã—(Yâ†’Y)Ã—(Yâ€³â†’Yâ€³)
   *)
  let _weird3 : exp = BigLambda("Y'",BigLambda("Y"
                    ,TyApply
                    (BigLambda("X",BigLambda("Y"
                      ,Pair(Pair(TyApply(pid,TVar("Y'")),TyApply(pid,TVar("X"))),TyApply(pid,TVar("Y")))))
                    ,TVar("Y")))) in
  let _weird3t : ty = Forall("Y'",Forall("Y",Forall("Y''"
                       ,Prod(Prod(Fun(TVar("Y'"),TVar("Y'")),Fun(TVar("Y"),TVar("Y"))),Fun(TVar("Y''"),TVar("Y''")))))) in
  TestBlock
  ( "INFER"
  , [ Let("x",Zero,Var("x"))                , R(Nat)
    ; Lambda("x",Nat,Var("x"))              , R(Fun(Nat,Nat))
    ; Lambda("x",TVar("X"),Var("x"))        , TypeError
    ; Apply(Lambda("x",Nat,Var("x")),Zero)  , R(Nat)
    ; Apply(Lambda("x",Nat,Var("x")),True)  , TypeError
    ; pid                                   , R(pidt)
    ; TyApply(pid,Nat)                      , R(Fun(Nat,Nat))
    ; TyApply(pid,Bool)                     , R(Fun(Bool,Bool))
    ; TyApply(pid,TVar("Y"))                , TypeError
    ; pidid                                 , R(pididt)
    ; TyApply(Apply(pidid,pid),Nat)         , R(Fun(Nat,Nat))
    ; ppid                                  , R(ppidt)
    ; TyApply(ppid,Nat)                     , R(pidt)
    ; fiveo                                 , R(fiveot)
    ; fiveo'                                , R(fiveo't)
    ; fiveobad                              , TypeError
    ; unp                                   , R(unpt)
    ; unp'                                  , R(unp't)
    ; bunp                                  , TypeError
    (* SANITY CHECKS FOR HELPER CODE --- NOT PART OF GRADE *)
    (*
    ; _pidpair                               , R(_pidpairt)
    ; _weird1                                , R(_pidpairt)
    ; _weird2                                , R(_weird2t)
    ; _weird3                                , R(_weird3t)
    *)
    ]
  , infer_test_result
  , (=)
  , [%show : exp]
  , [%show : ty test_result]
  )

let _ = 
  _SHOW_PASSED_TESTS := false ;
  run_tests [step_tests;infer_tests]

(* Name: <solution> *)
