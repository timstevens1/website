-- Demo material that accompanies the talk:
-- Constructive Galois Connections: With Applications to Abstracting Gradual Typing
-- David Darais
-- Jan 13 2016, University of Chile
--
-- This is a demonstration of constructive Galois connections (Darais
-- and Van Horn, ArXiv 2015) applied to Abstracting Gradual Typing
-- (Garcia, Clark and Tanter, POPL 2016).
--
-- This demo requires no dependencies or standard library, and was
-- written using Agda 2.4.2.5.
-- 
-- Author: David Darais

module Demo where

---------------------
-- A Small Prelude --
---------------------

data _â‰¡_ {ğ“} {A : Set ğ“} (x : A) : A â†’ Set ğ“ where
  refl : x â‰¡ x

-- These tell Agda to use _â‰¡_ as propositional equality during
-- dependent pattern matching and â‰¡-rewriting.

{-# BUILTIN EQUALITY _â‰¡_ #-}
{-# BUILTIN REFL refl #-}

--------------
-- AGT Demo --
--------------

-- The precise type system
data type : Set where
  Anyáµ– : type
  Noneáµ– : type
  âŸ¨ğ”¹âŸ©áµ– : type
  _âŸ¨â†’âŸ©áµ–_ : type â†’ type â†’ type

-- The precise metafunction that selects the domain of an arrow type
dom : type â†’ type
dom (Ï„â‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚) = Ï„â‚
dom Ï„ = Noneáµ–

-- The precise equate operator where
--   equate Ï„â‚ Ï„â‚‚ = Ï„â‚    ğ‘–ğ‘“ Ï„â‚ = Ï„â‚‚
--                  None  ğ‘–ğ‘“ Ï„â‚ â‰  Ï„â‚‚
equate : type â†’ type â†’ type
equate Anyáµ– Anyáµ– = Anyáµ–
equate Anyáµ– Ï„ = Noneáµ–
equate Noneáµ– Ï„ = Noneáµ–
equate âŸ¨ğ”¹âŸ©áµ– âŸ¨ğ”¹âŸ©áµ– = âŸ¨ğ”¹âŸ©áµ–
equate âŸ¨ğ”¹âŸ©áµ– Ï„ = Noneáµ–
equate (Ï„â‚â‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚â‚) (Ï„â‚â‚‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚â‚‚) = equate Ï„â‚â‚ Ï„â‚â‚‚ âŸ¨â†’âŸ©áµ– equate Ï„â‚‚â‚ Ï„â‚‚â‚‚
equate (Ï„â‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚) Ï„ = Noneáµ–

-- The gradual type system
-- ?, the "unknown" type, is written as âŠ¤áµ because it is the top of
-- the precision lattice
data typeâ™¯ : Set where
  âŠ¤áµ : typeâ™¯
  Anyáµ : typeâ™¯
  Noneáµ : typeâ™¯
  âŸ¨ğ”¹âŸ©áµ : typeâ™¯
  _âŸ¨â†’âŸ©áµ_ : typeâ™¯ â†’ typeâ™¯ â†’ typeâ™¯

-- The partial ordering relation on gradual types
data _âŠ‘_ : typeâ™¯ â†’ typeâ™¯ â†’ Set where
  âŠ¤áµ’ : âˆ€ {Ï„} â†’ Ï„ âŠ‘ âŠ¤áµ
  Anyáµ’ : Anyáµ âŠ‘ Anyáµ
  Noneáµ’ : Noneáµ âŠ‘ Noneáµ
  âŸ¨ğ”¹âŸ©áµ’ : âŸ¨ğ”¹âŸ©áµ âŠ‘ âŸ¨ğ”¹âŸ©áµ
  _âŸ¨â†’âŸ©áµ’_ : âˆ€ {Ï„â‚â‚ Ï„â‚‚â‚ Ï„â‚â‚‚ Ï„â‚‚â‚‚}
    â†’ Ï„â‚â‚ âŠ‘ Ï„â‚â‚‚
    â†’ Ï„â‚‚â‚ âŠ‘ Ï„â‚‚â‚‚
    â†’ (Ï„â‚â‚ âŸ¨â†’âŸ©áµ Ï„â‚‚â‚) âŠ‘ (Ï„â‚â‚‚ âŸ¨â†’âŸ©áµ Ï„â‚‚â‚‚)

-- The constructive abstraction function (like Î±)
Î· : type â†’ typeâ™¯
Î· Anyáµ– = Anyáµ
Î· Noneáµ– = Noneáµ
Î· âŸ¨ğ”¹âŸ©áµ– = âŸ¨ğ”¹âŸ©áµ
Î· (Ï„â‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚) = Î· Ï„â‚ âŸ¨â†’âŸ©áµ Î· Ï„â‚‚

-- The constructive concretization function, written as a relation
data _âˆˆÎ³[_] : type â†’ typeâ™¯ â†’ Set where
  âŠ¤á¶œ : âˆ€ {Ï„} â†’ Ï„ âˆˆÎ³[ âŠ¤áµ ]
  Anyá¶œ : Anyáµ– âˆˆÎ³[ Anyáµ ]
  Noneá¶œ : Noneáµ– âˆˆÎ³[ Noneáµ ]
  âŸ¨ğ”¹âŸ©á¶œ : âŸ¨ğ”¹âŸ©áµ– âˆˆÎ³[ âŸ¨ğ”¹âŸ©áµ ]
  _âŸ¨â†’âŸ©á¶œ_ : âˆ€ {Ï„â‚ Ï„â‚‚ Ï„â‚â™¯ Ï„â‚‚â™¯}
    â†’ Ï„â‚ âˆˆÎ³[ Ï„â‚â™¯ ]
    â†’ Ï„â‚‚ âˆˆÎ³[ Ï„â‚‚â™¯ ]
    â†’ (Ï„â‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚) âˆˆÎ³[ Ï„â‚â™¯ âŸ¨â†’âŸ©áµ Ï„â‚‚â™¯ ]

-- Soundness for the constructive Galois connection Î·â‡„Î³
sound[Î·Î³] : âˆ€ Ï„ â†’ Ï„ âˆˆÎ³[ Î· Ï„ ]
sound[Î·Î³] Anyáµ– = Anyá¶œ
sound[Î·Î³] Noneáµ– = Noneá¶œ
sound[Î·Î³] âŸ¨ğ”¹âŸ©áµ– = âŸ¨ğ”¹âŸ©á¶œ
sound[Î·Î³] (Ï„â‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚) = sound[Î·Î³] Ï„â‚ âŸ¨â†’âŸ©á¶œ sound[Î·Î³] Ï„â‚‚

-- Completeness for the constructive Galois connection Î·â‡„Î³
complete[Î·Î³] : âˆ€ {Ï„ Ï„â™¯} â†’ Ï„ âˆˆÎ³[ Ï„â™¯ ] â†’ Î· Ï„ âŠ‘ Ï„â™¯
complete[Î·Î³] âŠ¤á¶œ = âŠ¤áµ’
complete[Î·Î³] Anyá¶œ = Anyáµ’
complete[Î·Î³] Noneá¶œ = Noneáµ’
complete[Î·Î³] âŸ¨ğ”¹âŸ©á¶œ = âŸ¨ğ”¹âŸ©áµ’
complete[Î·Î³] (âˆˆÎ³â‚ âŸ¨â†’âŸ©á¶œ âˆˆÎ³â‚‚) =  complete[Î·Î³] âˆˆÎ³â‚ âŸ¨â†’âŸ©áµ’ complete[Î·Î³] âˆˆÎ³â‚‚

-- The gradual metafunction that selects the domain of an arrow type
domâ™¯ : typeâ™¯ â†’ typeâ™¯
domâ™¯ âŠ¤áµ = âŠ¤áµ
domâ™¯ (Ï„â‚ âŸ¨â†’âŸ©áµ Ï„â‚‚) = Ï„â‚
domâ™¯ Ï„áµ = Noneáµ

-- The proof of correctness of domâ™¯ in the constructive Galois
-- connection framework
correct[domâ™¯] : âˆ€ Ï„ â†’ Î· (dom Ï„) â‰¡ domâ™¯ (Î· Ï„)
correct[domâ™¯] Anyáµ– = refl
correct[domâ™¯] Noneáµ– = refl
correct[domâ™¯] âŸ¨ğ”¹âŸ©áµ– = refl
correct[domâ™¯] (Ï„â‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚) = refl

-- The gradual equate operator
equateâ™¯ : typeâ™¯ â†’ typeâ™¯ â†’ typeâ™¯
equateâ™¯ âŠ¤áµ Ï„ = Ï„
equateâ™¯ Ï„ âŠ¤áµ = Ï„
equateâ™¯ Anyáµ Anyáµ = Anyáµ
equateâ™¯ Anyáµ Ï„ = Noneáµ
equateâ™¯ Noneáµ Ï„ = Noneáµ
equateâ™¯ âŸ¨ğ”¹âŸ©áµ âŸ¨ğ”¹âŸ©áµ = âŸ¨ğ”¹âŸ©áµ
equateâ™¯ âŸ¨ğ”¹âŸ©áµ Ï„ = Noneáµ
equateâ™¯ (Ï„â‚â‚ âŸ¨â†’âŸ©áµ Ï„â‚‚â‚) (Ï„â‚â‚‚ âŸ¨â†’âŸ©áµ Ï„â‚‚â‚‚) = equateâ™¯ Ï„â‚â‚ Ï„â‚â‚‚ âŸ¨â†’âŸ©áµ equateâ™¯ Ï„â‚‚â‚ Ï„â‚‚â‚‚
equateâ™¯ (Ï„â‚ âŸ¨â†’âŸ©áµ Ï„â‚‚) Ï„ = Noneáµ

-- The proof of correctness of equateâ™¯ in the constructive Galois
-- connection framework
--
-- A much simpler proof for this exists which requires redefining
-- equateâ™¯ to have more structure in its definition. Brute-force
-- enumerating all cases is sufficient for now.
correct[equateâ™¯] : âˆ€ Ï„â‚ Ï„â‚‚ â†’ Î· (equate Ï„â‚ Ï„â‚‚) â‰¡ equateâ™¯ (Î· Ï„â‚) (Î· Ï„â‚‚)
correct[equateâ™¯] Anyáµ– Anyáµ– = refl
correct[equateâ™¯] Anyáµ– Noneáµ– = refl
correct[equateâ™¯] Anyáµ– âŸ¨ğ”¹âŸ©áµ– = refl
correct[equateâ™¯] Anyáµ– (Ï„â‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚) = refl
correct[equateâ™¯] Noneáµ– Anyáµ– = refl
correct[equateâ™¯] Noneáµ– Noneáµ– = refl
correct[equateâ™¯] Noneáµ– âŸ¨ğ”¹âŸ©áµ– = refl
correct[equateâ™¯] Noneáµ– (Ï„â‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚) = refl
correct[equateâ™¯] âŸ¨ğ”¹âŸ©áµ– Anyáµ– = refl
correct[equateâ™¯] âŸ¨ğ”¹âŸ©áµ– Noneáµ– = refl
correct[equateâ™¯] âŸ¨ğ”¹âŸ©áµ– âŸ¨ğ”¹âŸ©áµ– = refl
correct[equateâ™¯] âŸ¨ğ”¹âŸ©áµ– (Ï„â‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚) = refl
correct[equateâ™¯] (Ï„â‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚) Anyáµ– = refl
correct[equateâ™¯] (Ï„â‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚) Noneáµ– = refl
correct[equateâ™¯] (Ï„â‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚) âŸ¨ğ”¹âŸ©áµ– = refl
correct[equateâ™¯] (Ï„â‚â‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚â‚) (Ï„â‚â‚‚ âŸ¨â†’âŸ©áµ– Ï„â‚‚â‚‚) rewrite correct[equateâ™¯] Ï„â‚â‚ Ï„â‚â‚‚ | correct[equateâ™¯] Ï„â‚‚â‚ Ï„â‚‚â‚‚ = refl

-- Exercises:
-- 
-- * Easy: Define the subtyping relation <: on precise types.
-- * Moderate: Define the subtyping relation <:â™¯ on gradual types.
-- * Hard: Prove the subtyping relation <:â™¯ correct using Î· and/or Î³.
--
-- * Moderate: Define âˆ¨: and âˆ§:, the join and meet for the subtyping relation <: on precise types.
-- * Hard: Define âˆ¨:â™¯ and âˆ§:â™¯, the join and meet for the subtyping relation <:â™¯ on gradual types.
-- * Very Hard: Prove âˆ¨:â™¯ and âˆ§:â™¯ correct using Î· and/or Î³.
--
-- * Very Hard: Define a term language and substitution operation for
--   the precise language, and show that the subtyping relation <: is
--   safe for substitution.
-- * Very Hard: Define a term language and substitution operation for
--   the gradual language (in AGT style), and show that the abstract
--   subtyping relation <:â™¯ is safe for substitution.
-- * Very Hard: Prove substitution in the gradual language correct as
--   an abstraction of precise substitution.
